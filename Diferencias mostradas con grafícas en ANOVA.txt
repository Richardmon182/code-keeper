/*st102s01.sas*/  /*Part A*/
/*Use PROC MEANS to generate descriptive statistics for the four groups, 
and use PROC SGPLOT to produce box plots of bulb weight for the four groups*/

proc means data=stat1.garlic; 
   var BulbWt;
   class Fertilizer;
   title 'Descriptive Statistics of BulbWt by Fertilizer';
run;

proc sgplot data=stat1.garlic;
    vbox BulbWt / category=Fertilizer 
                  connect=mean;
    title "Bulb Weight Differences across Fertilizers";
run;

title;


/*st102s01.sas*/  /*Part B*/
ods graphics;

proc glm data=stat1.garlic plots=diagnostics;
    class Fertilizer;
    model BulbWt=Fertilizer;
    means Fertilizer / hovtest=levene;
    title "One-Way ANOVA with Fertilizer as Predictor";
run;
quit;

title;
